# 6. Conditional Processing

This chapter introduces a major item, giving programs the ability to make decisions. Introduction to Boolean operations are given, which are the core of all decision statements because they affect the CPU status flags. Use of conditional jump and loop instructions that interpret CPU status flags is shown.

A programming language that permits decision making lets you alter the flow of control, using a technique known as ***conditional branching***. Every `if` statement, `switch` statement, or conditional loop found in high level languages has built-in branching logic.

Programs that deal with hardware devices must be able to manipulate individual bits in numbers. Individual bits must be tested, cleared, and set. Data encryption and compression also rely on bit manipulation.

**Previous Chapter**: [[5.  Procedures]]

## 6.1 Boolean and Comparison Instructions

The intel instruction set contains the `AND`, `OR`, `XOR`, and `NOT` instructions, which directly implement Boolean operation on binary bits. In addition, the `TEST` instruction is a nondestructive Boolean `AND` operation.

| Instruction | Description |
| :-: | :-- |
| `AND` | Boolean `AND` operation between a source operand and a destination operand. |
| `OR` | Boolean `OR` operation between a source operand and a destination operand. |
| `XOR` | Boolean `XOR` operation between a source operand and a destination operand. |
| `NOT` | Boolean `NOT` operation on a destination operand. |
| `TEST` | Implied Boolean `AND` operation between a source and destination operand, setting the CPU flags appropriately. |

### The CPU Status Flags

- The **Zero flag** is set when the result of an operation equals zero.
- The **Carry flag** is set when an operation generates a carry out of the highest bit of the destination operand.
- The **Sign flag** is a copy of the high bit of the destination operand, indicating that it is negative if *set* and positive if *clear*, where zero is assumed to be positive.
- The **Overflow flag** is set when an instruction generates a result that is outside the signed range of the destination operand.

### AND Instruction

The `AND` instruction performs a Boolean (bit-wise) AND operation between each pair of matching bits in two operands and places the result in the destination operand.

```assembly
AND destination, source
...
AND reg, reg
AND reg, mem
AND reg, imm
AND mem, reg
AND mem, imm
```

The operands can be 8, 16, 32, or 64 bits, but they must be the same size.

| x | y | x & y |
| :-: | :-: | :-: |
| 0 | 0 | 0 |
| 0 | 1 | 0 |
| 1 | 0 | 0 |
| 1 | 1 | 1 |

The `AND` instruction lets you clear 1 or more bits in an operand without affecting the other bits. This technique is called *bit masking*.

```assembly
and AL, 11110110b  ; clear bits 0 and 3, leave others unchanged
```

#### AND Flags

The `AND` instruction always clear the Overflow and Carry flags. It modifies the Sign, Zero, and Parity flags in a way that is consistent with the value assigned to the destination operand.

#### Converting Characters to Uppercase

The `AND` instruction provides an easy way to translate a letter from lowercase to uppercase. If we compare the ASCII codes of capital **A** and lowercase **a**, it becomes clear that only bit 5 is different:

```assembly
0 1 1 0 0 0 0 1 = 61h ('a')
0 1 0 0 0 0 0 1 = 61h ('A')
```

If we `AND` any character with 11011111b, all bits are unchanged except for bit 5, which is cleared.

```assembly
.data
array BYTE 50 DUP(?)

.code
 mov ecx, LENGTHOF array
 mov esi, OFFSET array
L1:
 and BYTE PTR [esi], 1101111b ; clear bit 5
 inc esi
 loop L1
```

### OR Instruction

The `OR` instruction performs a Boolean (bit-wise) OR operation between each pair of matching bits in two operands and places the result in the destination operand.

```assembly
OR destination, source
...
OR reg, reg
OR reg, mem
OR reg, imm
OR mem, reg
OR mem, imm
```

The operands can be 8, 16, 32, or 64 bits, but they must be the same size.

| x | y | x \| y |
| :-: | :-: | :-: |
| 0 | 0 | 0 |
| 0 | 1 | 1 |
| 1 | 0 | 1 |
| 1 | 1 | 1 |

The `OR` instruction is particularly useful when you need to set 1 or more bits in an operand without affecting any other bits.

```assembly
or AL, 00000100b  ; set bit 2, leave others unchanged
```

#### OR Flags

The `OR` instruction always clear the Overflow and Carry flags. It modifies the Sign, Zero, and Parity flags in a way that is consistent with the value assigned to the destination operand.

For example, you can `OR` a number with itself (or zero) to obtain certain information about its value.

```assembly
or al, al
```

The values of the Zero and Sign flags indicate the following about the contents of AL:

| Zero Flag | Sign Flag | Value in AL is... |
| :-: | :-: | :-: |
| Clear | Clear | Greater than zero |
| Set | Clear | Equal to zero |
| Clear | Set | Less than zero |

### Bit-Mapped Sets

A ***bit-mapped set*** implements a one-to-one correspondence between a sequence of binary bits and set membership. Rather than holding pointers or references to objects in a container such as a [Java HashSet](https://docs.oracle.com/javase/8/docs/api/java/util/HashSet.html), an application can use a ***[bit vector](https://en.wikipedia.org/wiki/Bit_array)*** to map the binary number to an array objects.

For example, the following binary number uses bit positions numbered from 0 on the right to 31 on the left to indicate that array elements 0, 1, 2, and 31 are members of the set named **SetX**:

```assembly
SetX = 10000000 0000000 0000000 00000111
```

We can easily check for set membership and `AND`-ing a particular  member's bit position with a 1:

```assembly
mov eax, SetX
and eax, 10000b  ; is element[4] a member of SetX?
```

If the `AND` instruction in this example clears the Zero flag, we know element[4] is a member of SetX.

#### Set Complement

The complement of a set can be generated using the `NOT` instruction, which reverses all bits.

#### Set Intersection

The `AND` instruction produces a bit vector that represents the intersection of two set.

#### Set Union

The `OR` instruction produces a bit map that represents the union of two sets.

### XOR Instruction

The `XOR` instruction performs a Boolean (bit-wise) XOR operation between each pair of matching bits in two operands and places the result in the destination operand.

```assembly
XOR destination, source
...
XOR reg, reg
XOR reg, mem
XOR reg, imm
XOR mem, reg
XOR mem, imm
```

The operands can be 8, 16, 32, or 64 bits, but they must be the same size.

| x | y | x ^ y |
| :-: | :-: | :-: |
| 0 | 0 | 0 |
| 0 | 1 | 1 |
| 1 | 0 | 1 |
| 1 | 1 | 0 |

#### XOR Flags

The `XOR` instruction always clear the Overflow and Carry flags. It modifies the Sign, Zero, and Parity flags in a way that is consistent with the value assigned to the destination operand.

#### Checking the Parity Flag

In x86 processors, the Parity flag is set when the lowest byte of the destination operand of a bit-wise or arithmetic operation has even parity. Conversely, when the operand has odd parity, the flag is cleared.

An effective way to check the parity of a number, without changing its value is to exclusive-OR the number with zero.

```assembly
mov al, 10110101b    ; 5 bits = odd parity
xor al, 0            ; parity flag clear (odd)
mov al, 11001100b    ; 4 bits = even parity
xor al, 0            ; parity flag set
```

#### 16-Bit Parity

You can check the parity of a 16-bit integer by performing an exclusive-OR between the upper and lower bytes.

```assembly
mov ax, 64C1h   ; 0110 0100 1100 0001
xor ah, al      ; parity flag set (even)
```

### NOT Instruction

The `NOT` instruction inverts all bits in an operand. The result is called the *one's complement*. The following operand types are permitted:

```assembly
NOT reg
NOT mem
```

#### Flags

No flags are affected by the `NOT` instruction.

### TEST Instruction

The `TEST` instruction performs an implied `AND` operation between each pair of matching bits in two operands and set the Sign, Zero, and Parity flags based on the value assigned to the destination operand. The only flags based on the value assigned to the destination operand. The only difference between `TEST` and `AND` is that `TEST` does not modify the destination operand.

`TEST` is particularly valuable for finding out whether individual bits in an operand are set.

#### Flags

The `TEST` instruction always clears the Overflow and Carry flag. It modifies the Sign, Zero, and Parity flags in the same way as the `AND` instruction.

### CMP Instruction

The `CMP` (compare) instruction performs an implied subtraction of a source operand from a destination operand. Neither operand is modified:

```assembly
CMP destination, source
```

`CMP` uses the same operand combinations as the `AND` instruction.

#### Flags

The `CMP` instruction changes the Overflow, Sign, Zero, Carry, Auxiliary Carry, and Parity flags according to the value the destination operand would have had if actual subtraction had taken place. When two unsigned operands are compared, the Zero and Carry flags indicate the following relations between operands:

| `CMP` Results | ZF | CF |
| :-: | :-: | :-: |
| Destination < source | 0 | 1 |
| Destination > source | 0 | 0 |
| Destination = source | 1 | 0 |

When two signed operands are compared, the Sign, Zero, and Overflow flags indicate the following relations between operands:

| `CMP` Results | Flags |
| :-: | :-: |
| Destination < source | SF != OF |
| Destination > source | SF = OF |
| Destination = source | ZF = 1 |

### Settings and Clearing Individual CPU Flags

To set the Carry flag, use the `STC` instruction; to clear the Carry flag, use `CLC`:

```assembly
stc    ; set Carry flag
clc    ; clear Carry flag
```

To set the Overflow flag, add two positive values that produce a negative sum. To clear the Overflow flag, `OR` an operand with 0:

```assembly
mov al, 7Fh     ; AL = +127
inc al          ; AL = 80h (-128), OF = 1
eax, 0          ; clear Overflow flag
```

## 6.2 Conditional Jumps

### Conditional Structures

There are no block-oriented conditional instructions such as IF, ELSE, and ENDIF in the x86 instruction set, but you can implement useful conditional structures, as we will call them, using a combination of comparisons and jumps.

Two steps are involved in executing a conditional structure:
1. An instruction such as `CMP`, `AND`, or `SUB` modifies the CPU status flags.
2. A conditional jump instruction tests the flags and causes a branch to a new address.

### Jcond Instruction

A *conditional jump instruction* branches to a destination label when a status flag condition is true. Otherwise, if the flag condition is false, the instruction immediately following the conditional jump is executed. The syntax is as follows:

```assembly
Jcond destination
```

*cond* refers to a flag condition identifying the state of one or more flags. The following examples are based on the Carry and Zero flags:

| Jcond | Description | Flags |
| :-: | :-: | :-: |
| `JC` | Jump if carry | Carry flag set |
| `JNC` | Jump if not carry | Carry flag clear |
| `JZ` | Jump if zero | Zero flag set |
| `JNZ` | Jump if not zero | Zero flag clear |

#### Using the CMP Instruction

