This chapter introduces some essential instructions for transferring data and performing arithmetic. Shown is how to create loops, and use of some of the basic operators, such as `OFFSET`, `PTR`, and `LENGTHOF`.

**Previous Chapter**: [[3. Assembly Language Fundamentals]]

# 4.1 Data Transfer Instructions

A ***data transfer instruction*** copies data from a source operand to a destination operand. Assemblers let you do just about anything you want, as long as the processor's instruction set can do what you ask. Assembly language forces you to pay attention to data storage and machine-specific details. You must understand the processor's limitations when you write assembly code.

### Memory to Memory

A single `MOV` instruction cannot be used to move data directly from one memory location to another. Instead, you must move the source operand's value to a register before assigning its value to a memory operand:

```
.data
var1 WORD ?
var2 WORD ?
.code
mov ax, var1
mov var2, ax
```

### Overlapping Values

The following code example shows how the same 32-bit register can be modified using differently sized data.

```
.data
oneByte BYTE 78h
oneWord WORD 1234h
oneDword DWORD 12345678h
.code
mov eax, 0          ; EAX = 00000000h
mov al, oneByte     ; EAX = 00000078h
mov ax, oneWord     ; EAX = 00001234h
move eax, oneDword  ; EAX = 12345678h
mov ax, 0           ; EAX = 12340000h
```

## Zero / Sign Extension of Integers

### MOVZX Instruction

The `MOVZX` instruction (*move with zero-extend*) copies the contents of a source operand into a destination operand and zero-extends the value to 16 or 32 bits. The instruction is only used with *unsigned integers*.

### MOVSX Instruction

The `MOVSX` instruction (*move with sign-extend*) copies the contents of a source operand into the destination operand and sign-extends the value to 16 or 32 bits. This instruction is only used with *signed integers*.

### LAHF and SAHF Instructions

The `LAHF` (load status flags into AH) instructions copies the low byte of the EFLAGS register into AH. The following flags are copied: Sign, Zero, Auxiliary Carry, Parity, and Carry. Using this instruction, you can easily save a copy of the flags in a variable for safekeeping:

```
.data
saveFlags BYTE ?
.code lahf          ; load flags into AH
mov saveflags, ah   ; save them in a variable
```

The `SAHF` (store AH into status flags) instruction copies AH into the low byte of the EFLAGS (or RFLAGS) register. For example, you can retrieve the value of flags saved earlier in a variable:

```
mov ah, saveflags   ; load saved flags into AH
sahf                ; copy into flags register
```

### XCHG Instruction

The `XCHG` (exchange data) instruction exchanges the contents of two operands. There are three variants:

```
XCHG reg, reg
XCHG reg, mem
XCHG mem, reg
```

The rules for operands in the `XCHG` instruction are the same for those for the `MOV` instruction, except that `XCHG` does not accept ***immediate operands***. In array sorting applications, `XCHG` provides a simple way to exchange two array elements. Here are a few examples using `XCHG`:

```
xchg ax, bx    ; exchange 16-bit regs
xchg ah, al    ; exchange 8-bit regs
xchg var1, bx  ; exchange 16-bit mem op with BX
xchg eax, ebx  ; exchange 32-bit regs
```

To exchange two memory operands, use a register as a temporary container and combine `MOV` with `XCHG`:

```
mov  ax, val1
xchg ax, val2
mov  val1, ax
```

## Direct-Offset Operands

You can add a displacement to the name of a variable, creating a ***direct-offset operands***. This lets you access memory locations that may not have have explicit labels.

```
arrayB BYTE 10h, 20h, 30h, 40h, 50h
```

We can access the second byte in the array by adding 1 to the offset of `arrayB`:

```
mov al, [arrayB + 1]
```

An expression such as `arrayB + 1` produces what is called an ***effective address*** by adding an integer constant to the name of a data label. Surrounding an effective address with brackets makes it clear that the expression is dereferenced to obtain the contents of memory at the address. They are not required, but *should* be used for clarity.

Keep in mind that MASM has no built-in range checking for effective addresses, so you can easily retrieve a byte of memory from outside the array. Additionally, for non-`byte` arrays, the offsets must be multiples of however large one unit of the array is.

# 4.2 Addition and Subtraction

## INC and DEC Instructions

The `INC` (increment) and `DEC` (decrement) instructions, respectively, add 1 and subtract 1 from a register or memory operand. The syntax is 

```
INC reg/mem
DEC reg/mem
```

Here are some examples:

```
.data
myWord WORD 1000h
.code
inc myWord    ; myWord = 1001h
mov bx, myWord
dec bx        ; BX = 1000h
```

## ADD Instruction

The `ADD` instruction adds a source operand to a destination operand. The syntax is:

```
ADD dest, source
```

The source is unchanged and the sum is stored in the destination operand. The operand rules are the same for `ADD` as `MOV`.

## SUB Instruction

The `SUB` instruction subtracts a source operand from a destination operand. The set of possible operands is the same as for the `ADD` instruction. The syntax is:

```
SUB dest, source
```

The source is unchanged and the difference is stored in the destination operand. The operand rules are the same for `SUB` as `MOV`.

## NEG Instruction

The `NED` (negate) instruction reverses the sign of a number by converting the number to its two's complement. The following operands are permitted:

```
NEG reg
NEG mem
```

## Flags Affected by Addition and Subtraction

- The **Carry flag** indicates *unsigned* integer overflow.
- The **Overflow flag** indicates *signed* integer overflow.
- The **Zero flag** indicates that an operation produced zero.
- The **Sign flag** indicates that an operation produced a negative result.
- The **Parity flag** indicates whether or not an even number of 1 bits occurs in the least significant byte of the destination operand, immediately after an arithmetic or boolean instruction has been executed.
- The **Auxiliary Carry flag** is set when a 1 bit carries out of position 3 in the least significant byte of the destination operand.

### Unsigned Operations: Zero, Carry, and Auxiliary Carry

#### Zero Flag

The *Zero flag* is set when the result of an arithmetic operation equals zero.

```
mov ecx,1 
sub ecx,1            ; ECX = 0, ZF = 1 
mov eax,0FFFFFFFFh 
inc eax              ; EAX = 0, ZF = 1 
inc eax              ; EAX = 1, ZF = 0 
dec eax              ; EAX = 0, ZF = 1
```

#### Addition and the Carry Flag

When adding two unsigned integers, the *carry flag* is a copy of the carry out of the most significant bit of the destination operand.

```
mov al,0FFh 
add al,1            ; AL = 00, CF = 1
```

#### Subtraction and the Carry Flag

A subtract operation sets the *carry flag* when a larger unsigned integer is subtracted from a smaller one.

```
mov al,1 
sub al,2            ; AL = FFh, CF = 1
```

##### TIP
> The `INC` and `DEC` instruction do **not** affect the Carry flag. Applying the `NEG` instruction to nonzero operand *always* sets the Carry flag.

#### Auxiliary Carry

The *Auxiliary Carry flag* indicates a carry or borrow out of bit 3 in the destination operand. It is primarily used in [binary coded decimal](https://en.wikipedia.org/wiki/Binary-coded_decimal) arithmetic.

```
mov al,0Fh 
add al,1         ; AC = 1

  0 0 0 0 1 1 1 1
+ 0 0 0 0 0 0 0 1
------------------
  0 0 0 1 0 0 0 0
```

#### Parity

The *Parity flag* is set when the least significant byte of the destination has an even number of 1 bits. The following `ADD` and `SUB` instructions alter the parity of AL:

```
mov al, 10001100b
add al, 00000000b      ; AL = 10001110, PF = 1
sub al, 10000000b      ; AL = 00001110, PF = 0
```

### Signed Operations: Sign and Overflow Flags

#### Sign Flag

The **Sign flag** is set when the result of a signed arithmetic operation is negative. The next example subtracts larger integer (5) from a similar one (4):

```
mov eax, 4
sub eax, 5     ; EAX = -1, SF = 1
```

#### Overflow Flag

The **Overflow flag** is set when the result of a signed arithmetic operation overflows or underflows the destination operand.

```
mov al, +127
mov al, 1      ; OF = 1
```

```
mov al, -128
sub al, 1      ; OF = 1
```

#### The Addition Test

Overflow occurs when:
- adding two positive operands generates a negative sum
- adding two negative operands generates a positive sum

# 4.3 Data-Related Operators and Directives

Operators and directives are not executable instructions; instead they are interpreted by the assembler. You can use a number of assembly language directives to get information about the addresses and size characteristics of data:

- The `OFFSET` operator returns the distance of a variable from the beginning of its enclosing segment.
- The `PTR` operator lets you override an operand's default size.
- The `TYPE` operator returns the size (*in bytes*) of an operand or of each element in an array.
- The `LENGTHOF` operator returns the number of elements in an array.
- The `SIZEOF` operator returns the number of bytes used by an array initializer.

In addition, the `LABEL` directive provides a way to redefine the same variable with different size attributes. 

## OFFSET Operator

The `OFFSET` operator returns the offset of a data label. The offset represents the distance, in bytes, of the label from the beginning of the data segment.

![[img12.png]]

We declare three different types of variables:

```
.data
bVal  BYTE  ?
wVal  WORD  ?
dVal  DWORD ?
dval2 DWORD ?
```

If `bVal` were located at offset 00404000h, the `OFFSET` operator would return the following values:

```
mov esi, OFFSET bVal    ; ESI = 00404000h
mov esi, OFFSET wVal    ; ESI = 00404001h
mov esi, OFFSET dVal    ; ESI = 00404003h
mov esi, OFFSET dVal2   ; ESI = 00404007h
```

## ALIGN Directive

The `ALIGN` directive aligns a variable on a `byte`, `word`, `doubleword`, or paragraph boundary. The syntax is

```
ALIGN bound
```

*Bound* can be 1, 2, 4, 8, or 16. A value of 1 aligns the next variables on a 1-byte boundary. If *bound* is 2, the next variable is aligned on en even-numbered address. If *bound* is 4, the next address is a multiple of 4. If *bound* is 16, the next address is a multiple of 16, a paragraph boundary.

Why bother aligning data? Because the CPU can process data stored at even numbered addresses more quickly than those at odd-numbered addresses.

## PTR Operator

You can use the `PTR` operator to override the declared size of an operand. This is only necessary when you're trying to access the operand using a size attribute that is different from the one assumed by the assembler.

Suppose, for example, you would like to move the lower 16 bits of a doubleword variable named **myDouble** into AX. The assembler will not permit the following move because the operand sizes do not match:

```
.data
myDouble DWORD 12345678h
.code
mov ax, myDouble  ; ERROR
```

However, the `WORD PTR` operator makes it possible to move the low-order word (5678h) to AX:

```
mov ax, WORD PTR myDouble
```

We could also retrieve the 1234h, the word at location **myDouble + 2**, using the following statement:

```
mov ax, WORD PTR [myDouble+2]
```

### Moving Smaller Values into Larger Destinations

In the next example, the first word is copied to the lower half of EAX and the second word is copied to the upper half. The `DWORD PTR` operator makes this possible:

```
.data
wordList WORD 5678h, 1234h
.code
mov eax, DWORD PTR wordList   ; EAX = 12345678h
```

## TYPE Operator

The `TYPE` operator returns the size, in `bytes`, of a single element of a variable. For example, the `TYPE` of a `byte` equal 1, the `TYPE` of a word equals 2, the `TYPE` of a doubleword is 4, and the `TYPE` of a quadword is 8. Here are examples of each:

```
.data
var1 BYTE  ?
var2 WORD  ?
var3 DWORD ?
var4 QWORD ?
```

| Expression | Value |
| :-: | :-: |
| `TYPE` var1 | 1 |
| `TYPE` var2 | 2 |
| `TYPE` var3 | 4 |
| `TYPE` var4 | 8 |

## LENGTHOF Operator

The `LENGTHOF` operator counts the number of elements in an array, defined by the values appearing on the same line as its label. We will use the following data as an example:

```
.data
byte1    BYTE  10, 20, 30
array1   WORD  30 DUP(?), 0, 0
array2   WORD  5 DUP(3 DUP(?))
array3   DWORD 1, 2, 3, 4
digitStr BYTE  "12345678",0
```

| Expression | Value |
| :-: | :-: |
| `LENGTHOF` byte1 | 3 |
| `LENGTHOF` array1 | 30 + 2 |
| `LENGTHOF` array2 | 5 \* 3 |
| `LENGTHOF` array3 | 4 |
| `LENGTHOF` digitStr | 9 |

## SIZEOF Operator

The `SIZEOF` operator returns a value that is equivalent to multiplying `LENGTHOF` by `TYPE`.

```
.data
intArray WORD 32 DUP(0)
.code
mov eax, SIZEOF intArray   ; EAX = 64
```

## Label Directive

The `LABEL` directive lets you insert a label and give it a size attribute without allocating any storage. All standard size attributes can be used with `LABEL`, such as `BYTE`, `WORD`, `DWORD`, `QWORD`, or `TBYTE`. In the following example, we declare a label just before **val32** named **val16** and give it a `WORD` attribute:

```
.data
val16 LABEL WORD
val32 DWORD 12345678h
.code
mov ax, val16       ; AX = 5678h
mov dx, [val16+2]   ; DX = 1234h
```

**val16** is an alias for the same storage location as **val32**. The `LABEL` directive itself allocates no storage.

# 4.4 Indirect Addressing

Direct addressing is rarely used for array processing because it is impractical to use constant offsets to address more than a few array elements. Instead, we use a register as a pointer (called *indirect addressing*) and manipulate the register's value.

## Indirect Operands

### Protected Mode

An indirect operand can be any 32-bit general purpose register surrounded by brackets. The register is assumed to contain the address of some data.

```
.data
byteVal BYTE 10h
.code 
mov esi, OFFSET byteVal
mov al, [esi]             ; AL = 10h
```

If the destination operand uses indirect addressing, a new value is placed in memory at the location pointed to by the register. In the following example, the contents of the BL register are copied to the memory location addressed by ESI.

```
mov [esi], bl
```

### Using PTR with Indirect Operands

The size of an operand may not be evident from the context of an instruction.

```
inc [esi]   ; ERROR: operand must have size
```

The assembler does not know what ESI points to. The `PTR` operator confirms the operand size:

```
inc BYTE PTR [esi]
```

## Indexed Operands

An ***indexed operand*** adds a constant to a register to generate an effective address. Two basic formats are permitted by MASM, where the brackets are part of the notation:

```
constant[reg]
[constant + reg]
```

Indexed operands are ideally suited to array processing. The index register should be initialized to zero before accessing the first array element:

```
.data
arrayB BYTE 10h, 20h, 30h
.code
mov esi, 0
mov al, arrayB[esi]
```

## Pointers

A variable containing the address of another variable is called a ***pointer***. Pointers are great tools for manipulating arrays and data structures because the addresses they hold can be modified at runtime.

In the following 32-bit code example, **ptrB** contains the offset of **arrayB**:

```
.data
arrayB byte 10h, 20h, 30h, 40h
ptrB dword arrayB
```

We could also declare **ptrB** with the `OFFSET` operator to make the relationship more explicit:

```
ptrB dword OFFSET arrayB
```

### TYPEDEF Operator

The `TYPEDEF` operator lets you create a user-defined type that appears in the same context as built-in types when defining variables. `TYPEDEF` is ideal for creating point variables. For example:

```
PBYTE TYPEDEF PTR BYTE
```

This declaration would also variables to be defined using `PBYTE`:

```
.data
arrayB BYTE 10h, 20h, 30h, 40h
ptr1 PBYTE ?                    ; uninitialized
ptr2 PBYTE arrayB               ; points to an array
```

# 4.5 JMP and LOOP Instructions

Assembly language programs use conditional instructions to implement high-level statements such as IF statements and loops. A *transfer of control* or *branch*, is a way of altering the order in which statements are executed.

- ***Unconditional Transfer***: Control is transferred to a new location in all cases. The `JMP` instruction does this.
- ***Conditional Transfer***: The program branches if a certain condition is true.

## JMP Instruction

The `JMP` instruction cause an unconditional transfer to a destination, identified by a code label that is translated by the assembler into an offset.

```
JMP destination
```

```
top:
	.
	.
	jmp top   ; repeat the endless loop
```

`JMP` is unconditional, so a loop like this will continue endlessly unless another way is found to exit the loop

## LOOP Instruction

The `LOOP` instruction repeats a block of statements a specific number of time. ECX is automatically used as a counter and is decremented each time the loop repeats.

```
LOOP destination
```

A common programming error is to inadvertently initialize the ECX to zero before beginning a loop. If this happens, the `LOOP` instruction decrements ECX to FFFFFFFFh, and the loop repeats 4,294,967,296 times!

if you need to modify ECX inside a loop, you can save it in a variable at the beginning of the loop and restore it just before the `LOOP` instruction:

```
.data
count DWORD ?
.code
	mov ecx, 100     ; set loop count
top:
	mov count, ecx   ; save the count
	...
	mov ecx, 20      ; modify ECX
	...
	mov exc, count   ; restore loop count
	loop top
```

### Nested Loops

When creating a loop inside another loop, special consideration must be given to the outer loop counter in ECX. You can save it in a variable:

```
.data
count DWORD ?
.code
	mov ecx, 100     ; set outer loop count
L1:
	mov count, ecx   ; save outer loop count
	mov ecx, 20      ; set inner loop count
L2:
	...
	loop L2          ; repeat the inner loop
	mov ecx, count   ; restore outer loop count
	loop L1          ; repeat the outer loop
```

As a general rule, nested loops more than two levels deep are difficult to write. If the algorithm you're using requires deep loop nesting, move some of the inner loops into subroutines.