# 5. Procedures

This chapter introduces procedures, subroutines, and functions. A ***procedure*** is a block of code with a clearly marked beginning and end, that when called from some other place in a program, returns to its calling point when completed. Any program of reasonable size needs to be divided into parts, and certain parts need to be used more than once. Parameters can be passed in registers, and the CPU uses the runtime stack to track the calling location of procedures. Finally, two code libraries supplied, named Irvine32 and Irvine64, contain useful utilities that simplify input-output.

**Previous Chapter**: [[4. Data Transfers, Addressing, and Arithmetic]]

## 5.1 Stack Operations

A ***stack data structure*** follows the same principle as a stack of plates:

- New data values are added to the top of the stack.
- Existing values are removed from the top of the stack.
Stacks in general are useful structures for a variety of programming applications, and they can easily be implemented using [object-orientated programming methods](https://en.wikipedia.org/wiki/Object-oriented_programming). A stack is often called a **LIFO structure** (Last-in, First-out) because the last value put into the stack is always the first value taken out.

### Runtime Stack (32-Bit Mode)

The ***runtime stack*** is a memory array managed directly by the CPU, used to keep track of subroutine return addresses, procedure parameters, local variables, and other subroutine-related data.

In 32-bit mode, the ESP register, known as the ***stack pointer***, holds a 32-bit offset into some location on the stack. We rarely manipulate ESP directly; instead, it is indirectly modified by instructions such as `CALL`, `RET`, `PUSH`, and `POP`.

ESP always points to the last value to be added to, or *pushed* on, the top of stack.

![[img13.png]]

Each stack location in this figure contains 32-bits, which is the case when a program is running in 32-bit mode.

#### Push Operation

A ***push operation*** decrements the stack pointer (because the stack grows downward) by the appropriate amount according to the instruction operand's size and copies a value into the location in the stack referenced by the stack pointer.

Note that ESP register always points to the last item pushed on the stack.

![[img14.png]]

#### Pop Operation

A ***pop operation*** returns a copy of the value in the stack referenced by the stack pointer and increments the stack pointer by the appropriate amount according to the size of the instruction operand.

After the value is popped from the stack, the stack pointer is incremented to point to the next-highest location in the stack.

![[img15.png]]

#### Stack Applications

- A stack makes a convenient temporary save area for registers when they are used for more than one purpose. After they are modified, they can be restored to their original values.
- When the `CALL` instruction executes, the CPU saves the current subroutine's return address on the stack.
- When calling a subroutine, you pass input values called **arguments** by pushing them on the stack.
- The stack provides temporary storage for local variables inside subroutines.

### PUSH and POP Instructions

#### PUSH Instruction

The `PUSH` instruction first decrements ESP and then copies a source operand into the stack. A 16-bit operand causes ESP to be decremented by 2. A 32-bit operand causes ESP to be decremented by 4.

```assembly
PUSH reg/mem16
PUSH reg/mem32
PUSH imm32
```

*Note*: 8-bit (1 byte) values cannot be pushed onto the stack.

#### POP Instruction

The `POP` instruction first copies the contents of the stack element pointed to by ESP into a 16 or 32-bit destination operand, and then increments ESP. If the operand is 16 bits, ESP is incremented by 2; if the operand is 32 bits, ESP is incremented by 4.

```assembly
POP reg/mem16
POP reg/mem32
```

#### PUSHFD and POPFD Instructions

The `PUSHFD` instruction pushes the 32-bit EFLAGS registers on the stack and `POPFD` pops the stack into EFLAGS.

```assembly
pushfd
popfd
```

The `MOV` instruction cannot be used to copy the flags to a variable, so `PUSHFD` may be the best way to save the flags. There are times when it is useful to make a backup copy of the flags so you can restore them to their former values later. Often, we enclose a block of code within `PUSHFD` and `POPFD`:

```assembly
pushfd     ; save the flags
...
...
popfd      ; restore the flags
```

A less error-prone way to save and restore the flags is to push them on the stack and immediately pop them into a variable.

```assembly
.data
saveFlags DWORD ?
.code
pushfd          ; push flags on stack
pop saveFlags   ; copy into a variable
```

#### PUSHAD, PUSHA, POPAD, and POPA

The `PUSHAD` instruction pushes all of the 32-bit general-purpose registers on the stack in the following order: (1) EAX, (2) ECX, (3) EDX, (4) EBX, (5) ESP \[before executing `PUSHAD`], (6) EBP, (7) ESI, (8) EDI. The `POPAD` instruction pop the same registers off the stack in reverse order.

Similarly, the `PUSHA` instruction pushes the 16-bit general-purpose registers (AX, CX, DX, BX, SP, BP, SI, DI) on the stack in the order listed. The `POPA` instruction pops the same registers in reverse. You should only use `PUSHA` and `POPA` when programming in 16-bit mode.

The following code fragment demonstrates a common pattern in use of these instructions.

```assembly
MySub PROC
 pushad       ; save general-purpose registers
 ...
 mov eax, ...
 mov edx, ...
 mov ecx, ...
 ...
 popad        ; restore general-purpose registers
 ret
MySub ENDP
```

An important exception to the foregoing example is that procedures returning results in one or more registers should not use `PUSHA` or `PUSHAD`. The call to `POPA` or `POPAD` would overwrite a return value.

## 5.2 Defining and Using Procedures

In assembly language, we typically use the term ***procedure*** to mean the assembly language implementation of a subroutine subroutine. In other languages, subroutines are often called methods or functions.

### PROC Directive

#### Defining a Procedure

A procedure is declared using the `PROC` and `ENDP` directives. It must be assigned a valid identifier. When you create a procedure other than your program's startup procedure, end it with a `RET` instruction. `RET` forces the CPU to return to the location from where the procedure was called.

```assembly
sample PROC
 ...
 ret
sample ENDP
```

#### Labels in Procedures

By default, code labels are visible only within the procedure in which they are declared. It is possible to work around this limitation by declaring a ***global label***, identified by a double colon `::` after its name:

```assembly
GlobalLabel::
```

In terms of program design, it's not good practice to jump or loop outside of the current procedure.

#### Documenting Procedures

A good habit to cultivate is adding clear and readable documentation to your programs. The following are suggestions for information you can put at the beginning of each procedure:

- A description of all tasks accomplished by the procedure.
- A list of input parameters and their usage, labeled by a word such as **Receives**. If any input parameters have specific requirements for their input values, list them here.
- A description of any values returned by the procedure, labeled by a word such as **Returns**.
- A list of any special requirements, called ***preconditions***, that must be satisfied before the procedure is called. These can be labeled by the word **Requires**.

Example:

```assembly
;--------------------
SumOf Proc
; Calculates and returns the sum of three 32-bit integers.
; Receives:
;   EAX, EBX, ECX, the three integers. May be signed or unsigned.
; Returns:
;   EAX = sum
;--------------------
 add eax, ebx
 add eax, ecx
 ret
SumOf ENDP
```

### CALL and RET Instructions

The `CALL` instruction calls a procedure by directing the processor to begin execution at a new memory location. The procedure uses a `RET` instruction to begin the processor back to the point in the program where the procedure was called.

### Nested Procedure Calls

A ***nested procedure call*** occurs when a called procedure calls another procedure before the first procedure returns.

![[img16.png]]

Stack structures, in general, are use in situations where programs must retrace their steps in a specific order.

### Passing Register Arguments to Procedures

It's not a good idea to include references to specific variable names inside the procedure. In assembly language, it is common to pass arguments inside general-purpose registers.

For example, to call our simple procedure named **SumOf** that added the integers in the EAX, EBX, and ECX registers, we assign values to EAX, EBX, and ECX:

```assembly
.data?
theSum DWORD ?

.code
main PROC
 mov eax, 10000h
 mov ebx, 20000h
 mov ecx, 30000h
 call SumOf
 mov theSum, eax
```

### Saving and Restoring Registers

When possible, we save and restore registers modified by a procedure so the calling program can be sure that non of its own registers will be overwritten. The exception to this rule pertains to registers used as return values; do *not* push and pop them.

#### USES Operator

The `USES` operator, coupled with the `PROC` directive, conveniently lets you list the names of all registers you want to save and restore within a procedure. The `USES` operator immediately follow `PROC`, and is itself followed by a list of registers on the same line separated by spaces or tabs (not commas).

For example,

```assembly
ArraySum PROC uses esi ecx
 mov eax, 0
L1:
 add eax, [esi]
 add esi, TYPE DWORD
 loop L1
 ret
ArraySum ENDP
```

gets generated as

```assembly
ArraySum PROC
 push esi
 push ecx
 mov eax, 0
L1:
 add eax, [esi]
 add esi, TYPE DWORD
 loop L1
 pop ecx
 pop esi
 ret
ArraySum ENDP
```

##### Exception

There is an important exception to this rule about saving registers. The return register should not be push and popped; if it was, then the return value would get overridden.

## 5.3 Linking to an External Library

### Background Information

A ***link library*** is a file containing procedures that have been assembled into machine code. A link library begins as one or more source code files, which are assembled into object files. The object files are inserted into a link library file.

Suppose a program displays a string in the console window by calling a procedure named **WriteString**. The program source must contain a `PROTO` directive identifying the WriteString procedure, `WriteString proto`. Next, a `CALL` instruction executes **WriteString**, `call WriteString`. When the program is assembled, the assembler leaves the target address of the `CALL` instruction blank, hoping that it will be filled in by the linker. The linker looks for **WriteString** in the link library and copies the appropriate machine instructions from the library into the program's executable file. In addition, it inserts **WriteString's** address into the `CALL` instruction. If a procedure you're calling is not in the library, the linker issues an error message and does not generate an executable file.

#### Linker Command Options

The linker utility combines a program's object file with one or more object files and link libraries. The following command, for example, links `hello.obj` to the `irvine32.lib` and `kernel32.lib` library files:

```assembly
link hello.obj irvine32.lib kernel32.lib
```

## 5.4 The Irvine32 Library

Here is a complete list of the procedures defined in the Irvine32 Library. For more information, see [the library documentation](https://csc.csudh.edu/mmccullough/asm/help/).

| Procedure | Description |
| :-- | :-- |
| `Close File` | Closes a disk file that was previously opened. |
| `Clrscr` | Clears the console window and locates the cursor at the upper left corner. |
| `CreateOutputFile` | Creates a new disk file for writing in output mode. |
| `Crlf` | Writes an end-of-line sequence to the console window. |
| `Delay` | Pauses the program execution for a specified *n*-millisecond interval. |
| `DumpMem` | Writes a block of memory to the console window in hexadecimal. |
| `DumpRegs` | Displays the EAX, EBX, ECX, EDX, ESI, EDI, EBP, ESP, EFLAGS, and EIP registers in hexadecimal. Also displays the most common CPU status flags. |
| `GetCommandTail` | Copies the program's command-line arguments, called the *command tail*, into an array of bytes. |
| `GetDateTime` | Gets the current date and time from the system. |
| `GetMaxXY` | Gets the number of columns and rows in the console window's buffer. |
| `GetMseconds` | Gets the number of milliseconds elapsed since midnight. |
| `GetTextColor` | Gets the active foreground and background text colors in the console window. |
| `GotoXY` | Locates the cursor at a specific row and column in the console window. |
| `IsDigit` | Sets the Zero flag if the AL register contains the ASCII code for a decimal digit (0-9). |
| `MsgBox` | Displays a popup message box. |
| `MsgBoxAsk` | Displays a yes/no question in a popup message box. |
| `OpenInputFile` | Opens an existing disk file for input. |
| `ParseDecimal32` | Converts an unsigned decimal integer string to 32-bit binary. |
| `ParseInteger32` | Converts a signed decimal integer string to 32-bit binary. |
| `Random32` | Generates a 32-bit pseudorandom integer in the range 0 to FFFFFFFFh. |
| `Randomize` | Seeds the random number generator with a unique value. |
| `RandomRange` | Generates a pseudorandom integer within a specified range. |
| `ReadChar` | Waits for a single character to be typed at the keyboard and returns a single character. |
| `ReadDec` | Reads an unsigned 32-bit decimal integer from the keyboard, terminated by the Enter key. |
| `ReadFromFile` | Reads an input disk file into a buffer. |
| `ReadHex` | Reads a 32-bit hexadecimal integer from the keyboard, terminated by the Enter key. |
| `ReadInt` | Reads a 32-bit decimal integer from the keyboard, terminated by the Enter key. |
| `ReadKey` | Reads a character from the keyboard's input buffer without waiting for input. |
| `ReadString` | Reads a string from the keyboard, terminated by the Enter key. |
| `SetTextColor` | Sets the foreground and background colors of all subsequent text output to the console. |
| `Str_compare`  | Compares two strings. |
| `Str_copy` | Copies a source string to a destination string. |
| `Str_length` | Returns the length of a string in EAX. |
| `Str_trim` | Removes unwanted characters from a string. |
| `Str_ucase` | Converts a string to uppercase letters. |
| `WaitMsg` | Displays a message and waits for a key to be pressed. |
| `WriteBin` | Writes an unsigned 32-bit integer to the console window in ASCII binary format. |
| `WriteBinB` | Writes a binary integer to the console window in byte, word, or doubleword format. |
| `WriteChar` | Writes a single character to the console window. |
| `WriteDec` | Writes an unsigned 32-bit integer to the console window in decimal format. |
| `WriteHex` | Writes a 32-bit integer to the console window in hexadecimal format. |
| `WriteHexB` | Writes a byte, word, or doubleword integer to the console window in hexadecimal format.
| `WriteInt` | Writes a signed 32-bit integer to the console window in decimal format. |
| `WriteStackFrame` | Writes the current procedure's stack frame to the console. |
| `WritesStackFrameName` | Writes the current procedure's name and stack frame to the console. |
| `WriteString` | Writes a null-terminated string to the console window. |
| `WriteToFile` | Writes a buffer to an output file. |
| `WriteWindowsMsg` | Displays a string containing the most recent error generated by MS-Windows. |

## 5.5 64-bit Assembly Programming

### The Irvine64 Library

To see documentation on the Irvine64 Library, see [the documentation](https://www.google.com/search?q=irvine64+library).

## 5.6 Chapter Summary

The ***runtime stack*** is a special array that is used as a temporary holding area for addresses and data. The ESP register holds a 32-bit `OFFSET` into some location on the stack. The stack is called a ***LIFO structure*** because the last value placed in the stack is the first value taken out. A ***push operation*** copies a value into a stack. A ***pop operation*** removes a value from the stack and copies it to a register or variable. Stacks often hold procedure return addresses, procedure parameters, local variables, and registers used internally by procedures.

The `PUSH` instruction first decrements the stack pointer (because the stack grows down) and then copies a source operand into the stack. The `POP` instruction first copies the contents of the stack pointed to by ESP into a destination operand and then increments ESP.

The `PUSHAD` instruction pushes the 32-bit general purpose registers on the stack, and the `PUSHA` does the same for the 16-bit general-purpose registers. The `POPAD` instruction pops the stack into the 32-bit general purpose registers, and the `POPA` instruction does the same  for the 16-bit general-purpose registers. `PUSHA` and `POPA` should only be used for 16-bit programming.

The `PUSHFD` instruction pushes the 32-bit EFLAGS register on the stack, and `POPFD` pop the stack into EFLAGS. `PUSHF` and `POPF` do the same for the 16-bit FLAGS register.

A ***procedure*** is a named block of code declared using the `PROC` and `ENDP` directives. A procedure's execution ends with the `RET` instruction. The `CALL` instruction executes a procedure by inserting the procedure's address into the instruction pointer register. When the procedure finishes, the `RET` instruction brings the processor back to the point in the program from where the procedure was called. A ***nested procedure call*** occurs when a called procedure calls another procedure before it returns.

A code label followed by a single colon is only visible within its enclosing procedure. A code label followed by `::` is a global label, making it accessible from any statement in the same source code file.

The `USES` operator, coupled with the `PROC` directive, lets you list all registers modified by a procedure. The assembler generates code that pushes the registers at the beginning of the procedure and pops the registers before returning.

**Next Chapter**: [[6. Conditional Processing]]
